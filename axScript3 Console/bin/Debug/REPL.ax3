//REPL Script 1.0

#module <Ax/STD.dll>
#module <Ax/Exit.dll>

~(exit: type error |
	(
		~[ExitPoint]
		
		(set ^err error)
		
		(ifelse (eq type #EXITCODE_ERROR) {
			// Error
			(set ^err (string err))
			(set ^pos (err:IndexOf "--->"))
			(set ^pos (err:IndexOf "--->" (add pos 1)))
			(set ^pos (err:IndexOf "--->" (add pos 1)))
			(set ^endp (err:IndexOf "at" pos))
			
			(set ^err (err:Substring (add pos 5) (sub endp pos 10)))
			
			(if (eq err "System.Reflection.Target") {
				(set ^err "Invalid parameter type")
			})
		
			(printl "# {0}\n\n" err)
			(return false) // Keep it running.
		} {
			// Clean exit
			(printl "Goodbye.")
		})
	)
)

~(main: args |
	(
		~[EntryPoint]
		
		(consoletitle "axScript REPL 1.0")
		(loop)
	)
)

~(loop: 
	(	
		(set ^quit false)
		
		(do {
			(print "> ")
			(set ^inp (read))
			(ifelse (eq inp.Length 0) {
				(set ^quit true)
			} {
				(ifelse (eq inp "loop") {
					(printl "Yo dawg")
				} {
					(printl "{0}" (run inp))
				})
			})
		} %{eq quit false})
	)
)
